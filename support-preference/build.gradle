apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion

        consumerProguardFiles 'proguard-consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    resourcePrefix 'asp_'

    sourceSets {
        main {
            res.srcDirs += 'src/main/res-aosp'
            res.srcDirs += 'src/main/res-media'
        }
    }

    lintOptions {
        disable 'ResourceName', 'InlinedApi'
        warning 'GradleCompatible'
        checkReleaseBuilds = false
        abortOnError = false
        // Revert when lint stops with all the false positives >:-(
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }
}

dependencies {
    implementation "com.android.support:support-annotations:$supportLibVersionAnnotations"
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    api "com.android.support:appcompat-v7:$supportLibVersion"
    api "com.android.support:preference-v7:$supportLibVersion"

    implementation project(':support-spinner')
}

repositories {
    jcenter()
    google()
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

group = rootProject.GROUP_ID
version = rootProject.VERSION_NAME

apply from: rootProject.file('android-release.gradle')
